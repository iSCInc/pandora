apply plugin: 'application'
apply plugin: 'com.wikia.gradle.marathon'
apply plugin: 'maven-publish'

startScripts {
    doFirst {
        applicationDefaultJvmArgs.add('-Ddw.server.applicationConnectors[0].port=\$PORT0')
        applicationDefaultJvmArgs.add('-Ddw.server.adminConnectors[0].port=\$PORT1')
    }
    doLast {
        unixScript.text = unixScript.text.replace('port=\\\$', 'port=\$')
    }
}

artifacts {
    distZip
}
def archivaArtifactRepositoryUrl = "${archivaUrl}/repository/internal"

publishing {
    publications {
        applicationZip(MavenPublication) {
            artifact distZip
        }
    }
    repositories {
        maven {
            credentials {
                username archivaUsername
                password archivaPassword
            }
            url archivaArtifactRepositoryUrl
        }
    }
}

deployments {
    globalDefaults {
        app {
            dropwizardApplication()
            mavenSource(
                    archivaArtifactRepositoryUrl,
                    "publishApplicationZipPublicationToMavenRepository"
            )
        }
        healthchecks {
            healthcheck {
                portIndex = 1
                path = "/healthcheck"
            }
        }
        marathon {
            url = "http://mesos-s1.wikia-prod:8080"
        }
        resources {
            cpus = 0.1
            mem = 200
            instances = 1
            useRandomPorts(2)
        }
    }
    testing {
    }
}
