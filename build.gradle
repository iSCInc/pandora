buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven { url "${archivaUrl}/repository/internal" }
    }
    dependencies {
        classpath "com.wikia.gradle:marathon:0.5.2"
    }
}

subprojects {
    repositories {
        jcenter()
        mavenCentral();
    }
    apply plugin: 'java'
    apply from: rootProject.file("gradle/cobertura.gradle")
    sourceCompatibility = 1.8
    version = "0.1.1-SNAPSHOT"
    group = "com.wikia"

    task unitTest(type: Test) {
        useJUnit {
            excludeCategories 'com.wikia.pandora.core.test.IntegrationTest'
        }
    }

    task integrationTest(type: Test) {
        useJUnit {
            includeCategories 'com.wikia.pandora.core.test.IntegrationTest'
        }
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    artifacts {
        tests testJar
    }
}
