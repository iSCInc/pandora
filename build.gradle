buildscript {
    repositories {
        jcenter()
        mavenLocal()
        maven { url "${archivaUrl}/repository/internal" }
    }
    dependencies {
        classpath "com.wikia.gradle:marathon:0.5.3"
    }
}

subprojects {
    repositories {
        jcenter()
        mavenCentral();
        maven { url "${archivaUrl}/repository/internal" }
    }
    apply plugin: 'java'
    apply from: rootProject.file("gradle/cobertura.gradle")
    sourceCompatibility = 1.8
    version = "0.1.1-SNAPSHOT"
    group = "com.wikia"

    task unitTest(type: Test) {
        useJUnit {
            excludeCategories 'com.wikia.test.annotation.IntegrationTest'
        }
    }

    task integrationTest(type: Test) {
        useJUnit {
            includeCategories 'com.wikia.test.annotation.IntegrationTest'
        }
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    configurations {
        tests
    }

    artifacts {
        tests testJar
    }
    dependencies {
        testCompile project(":lib:test:annotation")
    }
}
